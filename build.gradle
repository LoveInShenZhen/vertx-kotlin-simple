buildscript {
    repositories {
        maven {
            name "nexus-163"
            url "http://mirrors.163.com/maven/repository/maven-public/"
        }
    }
    dependencies {
        classpath "io.ebean:ebean-gradle-plugin:11.10.1"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.61'
    id 'application'
}

apply plugin: 'io.ebean'
apply plugin: 'kotlin-kapt'

mainClassName = 'com.api.server.ApiServer'
// 可以在此添加jvm内存参数, eg: '-Xms512m', '-Xmx4096m'
applicationDefaultJvmArgs = ['-Duser.timezone=GMT+8', '-Dfile.encoding=UTF-8', '-Dsun.jnu.encoding=UTF-8']

defaultTasks 'tasks'

repositories {
    mavenLocal()
    maven {
        name "nexus-163"
        url "http://mirrors.163.com/maven/repository/maven-public/"
    }
    jcenter()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

distZip {
    into(project.name) {
        from '.'
        include 'conf/**'
    }
}

distTar {
    into(project.name) {
        from '.'
        include 'conf/**'
    }
}

tasks.withType(CreateStartScripts) {
    it.unixStartScriptGenerator.template = resources.text.fromFile('unixStartScript.txt')
}

tasks.withType(JavaExec) {
    if (project.hasProperty("debug")) {
        def debugPort = "5005"
        if (project.findProperty("debug").toString().isNumber()) {
            debugPort = project.findProperty("debug").toString()
        }
        it.jvmArgs.add("-Xdebug")
        it.jvmArgs.add("-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=${debugPort}")
        println("\u001b[35m" + "Remote debug is enabled. debug port: ${debugPort}" + "\u001b[0m")
    }

    it.systemProperties(System.properties as Map<String, ?>)
}

task usage {
    println('\u001b[32m 运行 (debug 模式, debug端口:5005) 命令           gradle run -Pdebug \u001B[0m')
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.61"
    compile "org.jetbrains.kotlin:kotlin-reflect:1.2.61"

    compile files("conf")

    compile group: 'com.github.kklongming', name: 'sz-scaffold', version: 'unspecified'
    compile group: 'com.github.kklongming', name: 'sz-ebean', version: 'unspecified'
    compile group: 'com.github.kklongming', name: 'sz-api-doc', version: 'unspecified'
    compile group: 'com.github.kklongming', name: 'sz-plan-task', version: 'unspecified'

    kapt "io.ebean:kotlin-querybean-generator:11.4.1"
}

ebean {
    debugLevel = 2
    queryBeans = true
    kotlin = true
    generatorVersion = "11.4.1"
}

sourceSets {
    main.java.srcDirs += [file("$buildDir/generated/source/kaptKotlin/main")]
}
