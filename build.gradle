plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.20'
    id 'application'
}

mainClassName = 'com.api.server.ApiServer'

defaultTasks 'build'

repositories {
    mavenLocal()
    maven {
        name "aliyun-maven"
        url "http://maven.aliyun.com/nexus/content/groups/public/"
    }
    jcenter()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

distZip {
    into(project.name) {
        from '.'
        include 'conf/*'
    }
}

distTar {
    into(project.name) {
        from '.'
        include 'conf/*'
    }
}

tasks.withType(CreateStartScripts) {
    it.unixStartScriptGenerator.template = resources.text.fromFile('unixStartScript.txt')
}

tasks.withType(JavaExec) {
    if (project.hasProperty("debug")) {
        def debugPort = "5005"
        if (project.findProperty("debug").toString().isNumber()) {
            debugPort = project.findProperty("debug").toString()
        }
        it.jvmArgs.add("-Xdebug")
        it.jvmArgs.add("-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=${debugPort}")
        println("\u001b[35m" + "Remote debug is enabled. debug port: ${debugPort}" + "\u001b[0m")
    }

    it.systemProperties(System.properties as Map<String, ?>)
}

task usage {
    println('\u001b[32m 运行 (debug 模式, debug端口:5005) 命令           gradle run -Pdebug \u001B[0m')
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect"

    compile files("conf")

    compile group: 'com.github.kklongming', name: 'sz-scaffold', version: 'unspecified'
    compile group: 'com.github.kklongming', name: 'sz-ebean', version: 'unspecified'
    compile group: 'com.github.kklongming', name: 'sz-api-doc', version: 'unspecified'
    compile group: 'com.github.kklongming', name: 'sz-plan-task', version: 'unspecified'
}
